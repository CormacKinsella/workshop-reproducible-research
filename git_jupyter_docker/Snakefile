rule get_SRA_by_accession:
    output:
        "data/raw_internal/{sra_id}.fastq.gz"
    params:
        maxReads=12000
    shell:
        """
        fastq-dump {wildcards.sra_id} -X {params.maxReads} --gzip -Z > {output}
        """

rule fastqc:
    input:
        "data/raw_internal/{sra_id}.fastq.gz"
    output:
        "results/{sra_id}.fastqc.html",
        "intermediate/{sra_id}.fastqc.tsv"
    shell:
        """
        fastqc {input} {output}
        """

rule download_genome:
    output:
        fasta="data/raw_external/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.fa.gz",
        annotation="data/raw_external/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.37.gff3.gz"
    shell:
        """
        wget ftp://ftp.ensemblgenomes.org/pub/bacteria/release-37/fasta/bacteria_18_collection/staphylococcus_aureus_subsp_aureus_nctc_8325/dna//Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.fa.gz -P data/
        wget ftp://ftp.ensemblgenomes.org/pub/bacteria/release-37/gff3/bacteria_18_collection/staphylococcus_aureus_subsp_aureus_nctc_8325//Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.37.gff3.gz -P data/
        """

rule index_genome:
    input:
        "data/raw_external/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.fa.gz"
    output:
        expand("intermediate/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.{n}.bt2",n=["1","2","3","4"]),
        expand("intermediate/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.rev.{n}.bt2",n=["1","2"])
    shadow: "shallow"
    shell:
        """
            gunzip -c {input} > tempfile
            bowtie2-build tempfile intermediate/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel
        """

rule align_to_genome:
    input:
        fastq="data/raw_internal/{sra_id}.fastq.gz",
        index=expand("intermediate/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.dna_rm.toplevel.{n}.bt2",n=["1","2","3","4"])
    output:
        temp("intermediate/{sra_id,\w+}.bam")
    run:
        indexBase = input.index[0].replace('.1.bt2','')
        shell("bowtie2 -x " + indexBase + " -U {input.fastq} > {output}")

rule sort_bam:
    input:
        "{prefix}.bam"
    output:
        "{prefix}.sorted.bam"
    shell:
        """
        samtools sort {input} > {output}
        """

rule generate_count_table:
    input:
        bams=expand("intermediate/{sra_ids}.sorted.bam",sra_ids=["SRR935090","SRR935091","SRR935092"]),
        annotation="data/raw_external/Staphylococcus_aureus_subsp_aureus_nctc_8325.ASM1342v1.37.gff3.gz"
    output:
        "data/final/counts.tsv"
    shadow: "shallow"
    shell:
        """
        gunzip -c {input.annotation} > tempfile
        htseq-count -f bam --idattr exon_id --additional-attr description {input.bams} tempfile > {output}
        """
